#!/usr/bin/env python
import os
import sys

env = SConscript("../godot-cpp/SConstruct")

target_path = "../../project/addons/gdpcurl/bin/"
curl_headers_path = ""
curl_library_path = ""
curl_library = ""
curl_deps = []

if env["platform"] == "linux":
    target_path += "linux/"
    curl_headers_path = "vendor/curl/include/"
    curl_library_path = "vendor/curl/lib/.libs/"
    curl_library = "libcurl"
elif env["platform"] == "windows":
    # WIP, we're moving to shared libs (hopefully)
    target_path += "windows/"
    curl_arch = "x64" if env["arch"] == "x86_64" else "x86"
    curl_headers_path = "vendor/curl/builds/libcurl-vc14-{}-{}-static-ipv6-sspi-schannel/include/".format(curl_arch, env["target"])
    curl_library_path = "vendor/curl/builds/libcurl-vc14-{}-{}-static-ipv6-sspi-schannel/lib/".format(curl_arch, env["target"])
    curl_library = "libcurl_a"

    if env["target"] == "debug":
        curl_library += "_debug"

env.Append(CPPPATH=[curl_headers_path])
env.Append(LIBPATH=[curl_library_path])
env.Append(LIBS=[curl_library])
env.Append(LIBS=curl_deps)

env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

library = env.SharedLibrary(
    target=target_path + "libgdpcurl.{}.{}.{}{}".format(
        env["platform"], env["target"], env["arch"], env["SHLIBSUFFIX"]
    ),
    source=sources,
)

Default(library)
