# This build script is adapted from the following projects released under the MIT License.
# godot-cpp <https://github.com/godotengine/godot-cpp>
# Copyright (c) 2017-2022 Godot Engine contributors.
# godot_xr_reference <https://github.com/GodotVR/godot_xr_reference>
# Copyright (c) 2021 Bastiaan Olij and contributors

import os
import platform
import sys

# Try to detect the host platform automatically.
# This is used if no `platform` argument is passed
if sys.platform.startswith("linux"):
    host_platform = "linux"
elif sys.platform == "win32" or sys.platform == "msys":
    host_platform = "windows"
else:
    raise ValueError("Could not detect platform automatically, please specify with " "platform=<platform>")

env = Environment(ENV=os.environ)

opts = Variables([], ARGUMENTS)

# Define our options
opts.Add(EnumVariable("target", "Compilation target", "debug", ["d", "debug", "r", "release"]))
opts.Add(EnumVariable("platform", "Compilation platform", host_platform, ["", "windows", "linux"]))
opts.Add(
    EnumVariable("p", "Compilation target, alias for 'platform'", host_platform, ["", "windows", "linux"])
)
opts.Add(BoolVariable("use_llvm", "Use the LLVM / Clang compiler", "no"))
opts.Add(PathVariable("target_path", "The path where the lib is installed.", "../../project/addons/gdpcurl/bin/", PathVariable.PathAccept))
opts.Add(PathVariable("target_name", "The library name.", "libgdpcurl", PathVariable.PathAccept))

# CPU architecture options.
architecture_array = ["", "universal", "x86_32", "x86_64", "arm32", "arm64", "rv64", "ppc32", "ppc64", "wasm32"]
architecture_aliases = {
    "x64": "x86_64",
    "amd64": "x86_64",
    "armv7": "arm32",
    "armv8": "arm64",
    "arm64v8": "arm64",
    "aarch64": "arm64",
    "rv": "rv64",
    "riscv": "rv64",
    "riscv64": "rv64",
    "ppcle": "ppc32",
    "ppc": "ppc32",
    "ppc64le": "ppc64",
}
opts.Add(EnumVariable("arch", "CPU architecture", "", architecture_array, architecture_aliases))

# Local dependency paths, adapt them to your setup
godot_headers_path = "../godot-cpp/godot-headers/"
cpp_bindings_path = "../godot-cpp/"
cpp_library = "libgodot-cpp"
curl_headers_path = ""
curl_library_path = ""
curl_library = ""
curl_deps = []

# Updates the environment with the option variables.
opts.Update(env)
# Generates help for the -h scons option.
Help(opts.GenerateHelpText(env))

# Process CPU architecture argument.
if env["arch"] == "":
    # No architecture specified. Default to the host architecture.
    host_machine = platform.machine().lower()
    if host_machine in architecture_array:
        env["arch"] = host_machine
    elif host_machine in architecture_aliases.keys():
        env["arch"] = architecture_aliases[host_machine]
    elif "86" in host_machine:
        # Catches x86, i386, i486, i586, i686, etc.
        env["arch"] = "x86_32"
    else:
        print("Unsupported CPU architecture: " + host_machine)
        Exit()

# We use this to re-set env["arch"] anytime we call opts.Update(env).
env_arch = env["arch"]

print("Building for architecture " + env["arch"] + " on platform " + env["platform"])

# This makes sure to keep the session environment variables on Windows.
# This way, you can run SCons in a Visual Studio 2017 prompt and it will find
# all the required tools
if host_platform == "windows":
    if env["arch"] == "x86_64":
        env = Environment(TARGET_ARCH="amd64")
    elif env["arch"] == "x86_32":
        env = Environment(TARGET_ARCH="x86")

    opts.Update(env)
    env["arch"] = env_arch

# Process some arguments
if env["use_llvm"]:
    env["CC"] = "clang"
    env["CXX"] = "clang++"

if env["p"] != "":
    env["platform"] = env["p"]

if env["platform"] == "":
    print("No valid target platform selected.")
    quit()

# For the reference:
# - CCFLAGS are compilation flags shared between C and C++
# - CFLAGS are for C-specific compilation flags
# - CXXFLAGS are for C++-specific compilation flags
# - CPPFLAGS are for pre-processor flags
# - CPPDEFINES are for pre-processor defines
# - LINKFLAGS are for linking flags

if env["target"] == "debug":
    env.Append(CPPDEFINES=["DEBUG_ENABLED", "DEBUG_METHODS_ENABLED"])

# Check our platform specifics
if env["platform"] == "linux":
    env["target_path"] += "linux/"
    cpp_library += ".linux"
    env.Append(CCFLAGS=["-fPIC"])
    env.Append(CXXFLAGS=["-std=c++17"])
    if env["target"] in ("debug", "d"):
        env.Append(CCFLAGS=["-g3", "-Og"])
    else:
        env.Append(CCFLAGS=["-g", "-O3"])
    
    curl_headers_path = "vendor/curl/include/"
    curl_library_path = "vendor/curl/lib/.libs/"
    curl_library = "libcurl"

elif env["platform"] == "windows":
    env["target_path"] += "windows/"
    cpp_library += ".windows"
    curl_deps += ["ws2_32", "wldap32", "advapi32", "crypt32", "normaliz"]
    
    # This makes sure to keep the session environment variables on windows,
    # that way you can run scons in a vs 2017 prompt and it will find all the required tools
    env.Append(ENV=os.environ)

    env.Append(CPPDEFINES=["WIN32", "_WIN32", "_WINDOWS", "_CRT_SECURE_NO_WARNINGS", "CURL_STATICLIB"])
    env.Append(CCFLAGS=["-W3", "-GR"])
    env.Append(CXXFLAGS=["-std:c++17"])
    if env["target"] in ("debug", "d"):
        env.Append(CPPDEFINES=["_DEBUG"])
        env.Append(CCFLAGS=["-EHsc", "-MDd", "-ZI", "-FS"])
        env.Append(LINKFLAGS=["-DEBUG"])
    else:
        env.Append(CPPDEFINES=["NDEBUG"])
        env.Append(CCFLAGS=["-O2", "-EHsc", "-MD"])

    if not(env["use_llvm"]):
        env.Append(CPPDEFINES=["TYPED_METHOD_BIND"])

    curl_headers_path = "vendor/curl/builds/libcurl-vc14-x64-debug-static-ipv6-sspi-schannel/include/"
    curl_library_path = "vendor/curl/builds/libcurl-vc14-x64-debug-static-ipv6-sspi-schannel/lib/"
    curl_library = "libcurl_a"

if env["target"] in ("debug", "d"):
    cpp_library += ".debug"

    if env["platform"] == "windows":
        curl_library += "_debug"
else:
    cpp_library += ".release"

cpp_library += "." + env["arch"]

# make sure our binding library is properly included
env.Append(CPPPATH=[".", godot_headers_path, cpp_bindings_path + "include/", cpp_bindings_path + "gen/include/", curl_headers_path])
env.Append(LIBPATH=[cpp_bindings_path + "bin/", curl_library_path])
env.Append(LIBS=[cpp_library, curl_library])
env.Append(LIBS=curl_deps)

# tweak this if you want to use different folders, or more folders, to store your source code in.
env.Append(CPPPATH=["src/"])
sources = Glob("src/*.cpp")

library = env.SharedLibrary(
    target=env["target_path"] + env["target_name"] + ".{}.{}.{}{}".format(
        env["platform"], env["target"], env["arch"], env["SHLIBSUFFIX"]
    ),
    source=sources,
)

Default(library)
